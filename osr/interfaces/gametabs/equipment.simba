{$DEFINE SRL_STATS_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL-T/osr.simba}
{$ENDIF}

type
  ERSEquipmentButton = (
    HELM,
    CAPE,
    AMULET,
    AMMO,
    WEAPON,
    BODY,
    SHIELD,
    LEGS,
    GLOVES,
    FEET,
    RING,
    
    STATS,
    PRICES,
    DEATH,
    FOLLOWER
  );
  
  TRSEquipment = record(TRSInterface)
    Slots: TBoxArray;
  end;



function TRSEquipment.GetSlotBoxes(): TBoxArray;
var
  tba: TBoxArray = [
    [80, 8, 30, 30],
    [39, 47, 30, 30],
    [81, 47, 30, 30],
    [121, 47, 30, 30],
    [24, 86, 30, 30],
    [81, 86, 30, 30],
    [136, 86, 30, 30],
    [80, 126, 30, 30],
    [24, 166, 30, 30],
    [80, 166, 30, 30],
    [136, 166, 30, 30]
  ];
  i: Int32;
begin
  if Self.Slots <> [] then
    Exit(Self.Slots);

  for i := 0 to High(tba) do
  begin
    tba[i].X2 += tba[i].X1;
    tba[i].Y2 += tba[i].Y1;
  end;

  Self.Slots := tba.Offset([Self.X1()-4, Self.Y1()]);
  Result := Self.Slots;
end;

procedure TRSEquipment.Setup(); override;
begin
  inherited;

  Self.Name := 'Equipment';
  Self.ItemFinder.Name := Self.Name + '.ItemFinder';
  Self.ItemFinder.GetSearchBoxesFunction := @Self.GetSlotBoxes;
end;

procedure TRSEquipment.SetupAlignment(Mode: ERSClientMode); override;
begin
  inherited;

  Self.Alignment.Left := [@GameTab.X1];
  Self.Alignment.Right := [@GameTab.X2];
  Self.Alignment.Top := [@GameTab.Y1];
  Self.Alignment.Bottom := [@GameTab.Y2];
end;

function TRSEquipment.IsOpen: Boolean;
begin
  Result := (GameTabs.GetCurrentTab() = ERSGameTab.EQUIPMENT);
end;

function TRSEquipment.Open: Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.EQUIPMENT);
end;

function TRSEquipment.GetButtons: TRSButtonArray;
begin
  Result := Self.FindButtons([[34,34], [40,40]]);
end;

function TRSEquipment.GetButton(Button: ERSEquipmentButton): TRSButton;
var
  Buttons: TRSButtonArray := Self.GetButtons();
begin
  if Length(Buttons) = Length(ERSEquipmentButton) then
    Result := Buttons[Button];
end;


function TRSEquipment.FindItem(Item: TRSItem; out box: TBox): Boolean;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.Find([item], box);
end;

//for use without having to pass extra parameters
function TRSEquipment.ContainsItem(Item: TRSItem): Boolean;
var
  B: TBox;
begin
  Result := Self.FindItem(Item, B);
end;

function TRSEquipment.FindItem(Item: TRSItem): Boolean; overload; deprecated 'Use TRSEquipment.ContainsItem()';
var
  B: TBox;
begin
  Result := Self.FindItem(Item, B);
end;


function TRSEquipment.ContainsAny(items: TRSItemArray): Boolean;
var
  item: TRSItem;
begin
  for item in items do
    if Self.ContainsItem(item) then
      Exit(True);
end;

function TRSEquipment.ContainsAll(items: TRSItemArray): Boolean;
var
  item: TRSItem;
begin
  Result := True;
  for item in items do
    if not Self.ContainsItem(item) then
      Exit(False);
end;

function TRSEquipment.HoverItem(Item: TRSItem): Boolean;
var
  B: TBox;
begin
  if Self.FindItem(Item, B) then
  begin
    Mouse.Move(B);
    Result := True;
  end;
end;

function TRSEquipment.ClickItem(Item: TRSItem; Option: String = ''): Boolean;
begin
  if Self.HoverItem(Item) then
  begin
    if Option <> '' then
      Result := ChooseOption.Select(Option)
    else
    begin
      Result := True;

      Mouse.Click(MOUSE_LEFT);
    end;
  end;
end;

//to count arrows, darts, etc.
function TRSEquipment.CountItemStack(item: TRSItem): Int32;
begin
  if not Self.IsOpen() then
    Exit;

  Result := Self.ItemFinder.CountStack(item);
end;


function TRSEquipment.IsSlotUsed(b: TBox): Boolean; overload;
begin
  // black with tolerance for when item is transparent
  Result := SRL.CountColor(CTS1(1118740, 35), b) > 0;
end;

function TRSEquipment.IsSlotUsed(Slot: ERSEquipmentButton): Boolean; overload;
begin
  Result := Self.Open and Self.IsSlotUsed(Self.GetButton(Slot).Bounds);
end;

function TRSEquipment.CountGear: Int32;
var
  i: ERSEquipmentButton;
begin
  for i := ERSEquipmentButton.HELM to ERSEquipmentButton.Ring do
    if Self.IsSlotUsed(i) then
      Result += 1;
end;


var
  Equipment: TRSEquipment;

procedure TRSClient.ClientModeChanged; override;
begin
  inherited;

  Equipment.SetupAlignment(Self.Mode);
end;

procedure TSRL.Setup; override;
begin
  inherited;

  Equipment.Setup();
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Equipment.Draw(Bitmap);
end;
